// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    // Versions used across modules
    ext.versions = [:]
    ext.versions.with {
        androidGradlePlugin = '3.1.0-alpha06'

        compileSdk = 27
        targetSdk = 27

        buildTools = '27.0.2'
        supportLibrary = '27.0.2'
        androidWearable = '2.1.0'
        timber = '4.5.1'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0-alpha06'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    // Force using our version of the support libraries, even for those we don't depend on directly (transitive dependencies)
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'com.android.support'
                        && details.requested.name != 'multidex'
                        && details.requested.name != 'multidex-instrumentation') {
                    details.useVersion versions.supportLibrary
                }
            }
        }
    }
}
